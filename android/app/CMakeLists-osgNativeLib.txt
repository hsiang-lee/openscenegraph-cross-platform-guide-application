cmake_minimum_required(VERSION 3.4.1)

# Path to external projects:
# * OpenSceneGraph
# * openscenegraph-cross-platform-guide-application
set(EXT_PROJ_DIR "${CMAKE_SOURCE_DIR}/../..")

# Specify critical OpenSceneGraph build variables.
set(OPENGL_PROFILE "GLES2" CACHE STRING "OpenGL variant to use")
set(DYNAMIC_OPENTHREADS OFF CACHE BOOL "Link OpenThreads statically")
set(DYNAMIC_OPENSCENEGRAPH OFF CACHE BOOL "Link OpenSceneGraph statically")
# Build and reference OpenSceneGraph.
set(OSG_SOURCE_DIR "${EXT_PROJ_DIR}/OpenSceneGraph")
set(OSG_BUILD_DIR "${EXT_PROJ_DIR}/OpenSceneGraph/build/${ANDROID_ABI}")
file(MAKE_DIRECTORY ${OSG_BUILD_DIR})
add_subdirectory(${OSG_SOURCE_DIR} ${OSG_BUILD_DIR})
include_directories(${OSG_SOURCE_DIR}/include)
include_directories(${OSG_BUILD_DIR}/include)

# Reference OpenSceneGraph cross-platform guide application.
include_directories("${EXT_PROJ_DIR}/openscenegraph-cross-platform-guide-application/android/src-gen")

# Build osgNativeLib.
add_library(osgNativeLib SHARED src/main/cpp/osgNativeLib.cpp)
find_library(log-lib log)
# Libraries must be linked in the specified order.
# Otherwise you may get unsatisified linker errors.
target_link_libraries(
    osgNativeLib
    ${log-lib}
    GLESv2
    osgViewer
    osgDB
    z
    # osgDB plugins start.
    osgdb_osg
    osgdb_serializers_osg
    # osgDB plugins end.
    osgGA
    osgShadow
    osgText
    osgUtil
    osg
    OpenThreads
)